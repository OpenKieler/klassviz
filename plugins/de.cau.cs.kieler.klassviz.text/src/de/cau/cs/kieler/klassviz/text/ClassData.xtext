/*
 * KlassViz - Kieler Class Diagram Visualization
 * 
 * A part of OpenKieler
 * https://github.com/OpenKieler
 * 
 * Copyright 2014, 2018 by
 * + Christian-Albrechts-University of Kiel
 *   + Department of Computer Science
 *     + Real-Time and Embedded Systems Group
 * 
 * This code is provided under the terms of the Eclipse Public License (EPL).
 * See the file epl-v10.html for the license text.
 */
grammar de.cau.cs.kieler.klassviz.text.ClassData with org.eclipse.xtext.common.Terminals

import "http://kieler.cs.cau.de/classdata" 
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

KClassModel returns KClassModel:
    {KClassModel}
    ('import'
        ('project' javaProjects+=STRING | 'bundle' bundles+=QualifiedID)
    )*
    (options+=KOption)*
    (packages+=KPackage)*;

KOption returns KOption:
    {KOption}
    key=QualifiedID '=' value=PropertyValue;

KPackage returns KPackage:
    'package' name=QualifiedID '{'
        (types+=KType)*
    '}';

KType returns KType:
    (KClass | KInterface | KEnum) ('{'
        (fields+=KField | methods+=KMethod | dependencies+=KDependency)*
    '}')?;

KClass returns KClass:
    'class' name=ID;

KInterface returns KInterface:
    'interface' name=ID;

KEnum returns KEnum:
    'enum' name=ID;

KDependency returns KDependency:
    'depend' (label=STRING)? target=[KType|ID];

KField returns KField:
    name=ID;

KMethod returns KMethod:
    name=ID '('
        (parameters+=KTypeReference ("," parameters+=KTypeReference)*)?
    ')';

KTypeReference returns KTypeReference:
    signature=ID;

//--------------- Terminals ----------------

QualifiedID returns ecore::EString:
    ID ('.' ID)*;

PropertyValue returns ecore::EString:
    BOOLEAN | STRING | Float | QualifiedID;

Float returns ecore::EFloat:
    TFLOAT | NATURAL;

terminal BOOLEAN returns ecore::EBoolean:
    'true' | 'false';

terminal TFLOAT returns ecore::EFloat:
    ('+' | '-') (
        ('0'..'9')+ ('.' ('0'..'9')*)? (('e' | 'E') ('+' | '-')? ('0'..'9')+)?
        | '.' ('0'..'9')+ (('e' | 'E') ('+' | '-')? ('0'..'9')+)?
    )
    | ('0'..'9')+ '.' ('0'..'9')* (('e' | 'E') ('+' | '-')? ('0'..'9')+)?
    | '.' ('0'..'9')+ (('e' | 'E') ('+' | '-')? ('0'..'9')+)?
    | ('0'..'9')+ ('e' | 'E') ('+' | '-')? ('0'..'9')+;
terminal NATURAL returns ecore::EInt:
    ('0'..'9')+;
